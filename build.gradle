apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'checkstyle'
apply plugin: 'application'
apply plugin: 'maven'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "org.manifold"
// name = "manifold-engine"
version = '0.1.4-SNAPSHOT'

mainClassName = "org.manifold.MainEngine"

jar {
  manifest {
    attributes 'Implementation-Title': 'Manifold Engine', 'Implementation-Version': version, 'Main-Class': 'org.manifold.MainEngine'
  }
}

repositories {
  mavenCentral()
  jcenter()
  maven {
    name 'Manifold release repository'
    url 'http://manifold-lang.org/releases'
  }
}

dependencies {
  compile 'com.google.code.gson:gson:2.2.4'
  compile 'com.google.guava:guava:17.0'
  testCompile 'junit:junit:4.11'
  compile 'log4j:log4j:1.2.16'
  compile group: 'commons-cli', name: 'commons-cli', version: '1.2'
  compile 'org.manifold:manifold-core:0.9.+'
}

buildscript {
  repositories {
    mavenCentral()
    maven {
        name 'JFrog OSS snapshot repo'
        url  'https://oss.jfrog.org/oss-snapshot-local/'
    }
    jcenter()
}

  dependencies {
    classpath 'org.ajoberstar:gradle-jacoco:0.2.0'
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:1.0.2'
  }
}

apply plugin: 'jacoco'

jacoco {
  toolVersion = "0.7.1.201405082137"
  reportsDir = file("$buildDir/reports/jacoco")
}

test {
  testLogging.showStandardStreams = true
  jacoco {
    append = false
    destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
    classDumpFile = file("$buildDir/jacoco/classpathdumps")
  }
}

jacocoTestReport {
  reports {
    xml.enabled = true
    csv.enabled = false
    html.destination = "${buildDir}/reports/jacoco"
  }
}

check.dependsOn(jacocoTestReport)

checkstyle {
  configFile = new File(rootDir, "checkstyle.xml")
  ignoreFailures = false
  showViolations = true
  toolVersion = "5.9"
}

task jenkinsTest {
  inputs.files test.outputs.files
  doLast {
    def timestamp = System.currentTimeMillis()
    testResultsDir.eachFile { it.lastModified = timestamp }
  }
}

build.dependsOn(jenkinsTest)

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file://localhost/var/www/releases")
    }
  }
}

startScripts {
  // classpath = files('$APP_HOME/lib/*')
  classpath += files('src/dist/PLACEHOLDER')
  doLast {
    def windowsScriptFile = file getWindowsScript()
    def unixScriptFile = file getUnixScript()
    windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\PLACEHOLDER', '%APP_HOME%\\lib\\*;')
    unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME/lib/PLACEHOLDER', '$APP_HOME/lib/*;')
  }
}
